# simple state space AR1 model for turtle nesting 
# original code from Lab 7, Introduction to Bayesian Time-Series
# analysis using Jags. Univ Washington: 
# https://www.scribd.com/document/373707632/Lab-7-Fitting-models-with-JAGS-pdf

# Also can be found here:
# https://nwfsc-timeseries.github.io/atsa-labs/sec-jags-uss.html
#
# For this model, I added another theta. Two thetas are used for increasing
# and decreasing months within each year. 
#
# In this variant, I use a negative binomial distribution instead of Normal
# for the observation model. Not done as of 9/19/2018

model{
	for (t in 2:T){
		# process
        theta[t] <- ifelse(m[t] < 8, theta.1, theta.2)
        predX[t] <- theta[t] * X[t-1]

        tau.pro[t] <- ifelse(m[t] < 9 && m[t] > 4, tau.pro1, tau.pro2)
        X[t] ~ dnorm(predX[t], tau.pro[t])T(0,)

		# observation
		predY[t] <- X[t]
		# should parameterize w.r.t. mean and variance?
		# mu_x = alpha/beta
		# var_x = (alpha/beta^2)(beta + 1)
		# p = beta/(beta+1)
		# r = alpha
		r[t] <- beta * X[t]
		
		y[t] ~ dnegbin(p, r[t])
    }

    X[1] <- mu
    predY[1] <- X[1]
	r[1] <- beta * X[1]
	y[1] ~ dnegbin(p, r[1])
	p <- beta/(beta + 1)

    mu ~ dnorm(0, 0.01)
    sigma.pro1 ~ dunif(0, 600)
    tau.pro1 <- 1/(sigma.pro1 * sigma.pro1)

    sigma.pro2 ~ dunif(0, 100)
    tau.pro2 <- 1/(sigma.pro2 * sigma.pro2)

    beta ~ dunif(0, 100)
    
    theta.1 ~ dnorm(0, 0.010)
    theta.2 ~ dnorm(0, 0.010)
}