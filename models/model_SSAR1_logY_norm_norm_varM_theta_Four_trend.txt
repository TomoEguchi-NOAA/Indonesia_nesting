# simple state space AR1 model for turtle nesting 
# original code from Lab 7, Introduction to Bayesian Time-Series
# analysis using Jags. Univ Washington: 
# https://www.scribd.com/document/373707632/Lab-7-Fitting-models-with-JAGS-pdf

# Also can be found here:
# https://nwfsc-timeseries.github.io/atsa-labs/sec-jags-uss.html
#
# For this model, the natural log of raw counts were used as data (y). The state model
# is normal and the observation model is norm. 
#
# To accommodate increasing and decreasing months and changes in variances, variance
# is treated as functions of months. The mean is modeled with a discrete Fourier series.
#
# I used quite informative prior for X[1] ~ dnorm(5, 0.1) although variance of 10 should 
# be wide enough in the log scale.  I used similar priors for the slopes: dnorm(0, 0.1).
# For all standard deviations (process and observation), I used uniform between 0 and 20, 
# which should be wide enough in the log scale. 
#
# I didn't see any reason to make the priors flatter because that would make convergence difficult
# and did not gain anything in return. 

model{
	for (t in 2:T){
		# process: a discrete Fourier series for the state slope
        theta[t] <- beta.cos * cos(2 * pi * m[t] / period) + beta.sin * sin(2 * pi * m[t] / period)
        predX[t] <- theta[t] + X[t-1]

        # variance
        tau.pro[t] <- tau.pro1[m[t]]
        X[t] ~ dnorm(predX[t], tau.pro[t])

		# observation
		y[t] ~ dnorm(X[t], tau.obs)
        
        loglik[t] <- logdensity.norm(y[t], X[t], tau.obs)
        
    }

    beta.cos ~ dnorm(0, 0.1)
    beta.sin ~ dnorm(0, 0.1)
    X[1] <- mu
    y[1] ~ dnorm(X[1], tau.obs)
    loglik[1] <- logdensity.norm(y[1], X[1], tau.obs)

    mu ~ dnorm(5, 0.1)
    
    for (k in 1:12){
        sigma.pro1[k] ~ dgamma(2, 0.5)
        tau.pro1[k] <- 1/(sigma.pro1[k] * sigma.pro1[k])
        theta.1[k] ~ dnorm(0, 1)
    }
    
    sigma.obs ~ dgamma(2, 0.5)
    tau.obs <- 1/(sigma.obs * sigma.obs)

}
